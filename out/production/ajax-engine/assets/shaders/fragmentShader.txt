#version 400 core

// THE FRAGMENT SHADER

in vec2 pass_textureCoords;
in vec3 surfaceNormal;
in vec3 towardsLightVector;
in vec3 towardsCameraVector;
in float visibility;

out vec4 out_Colour;

uniform sampler2D textureSampler;
uniform vec3 lightColour;
uniform float shineDamper;
uniform float reflectivity;
uniform float ambientLightFactor;
uniform vec3 skyColour;

void main(void){

    vec3 unitNormal = normalize(surfaceNormal);
    vec3 unitLightVector = normalize(towardsLightVector);

    float nDot1 = dot(unitNormal, unitLightVector);
    float brightness = max(nDot1, ambientLightFactor);
    vec3 diffuse = brightness * lightColour;

    vec3 unitTowardsCameraVector = normalize(towardsCameraVector);
    vec3 lightDirection = -unitLightVector;
    vec3 reflectedLightDirection = reflect(lightDirection, unitNormal);

    float specularFactor = dot(reflectedLightDirection, unitTowardsCameraVector);
    specularFactor = max(specularFactor, 0.0);
    float dampedFactor = pow(specularFactor, shineDamper);
    vec3 finalSpecular = dampedFactor * reflectivity * lightColour;

    vec4 textureColour = texture(textureSampler, pass_textureCoords);
    if(textureColour.a < 0.5){
        discard;
    }

    out_Colour = vec4(diffuse, 1.0) * textureColour + vec4(finalSpecular, 1.0);
    out_Colour = mix(vec4(skyColour, 1.0), out_Colour, visibility);

}